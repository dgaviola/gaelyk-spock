import org.apache.ivy.plugins.resolver.URLResolver

apply plugin: "groovy"
apply plugin: "eclipse"
apply plugin: 'idea'
archivesBaseName = "gaelyk-spock"
version = "0.2.1"

def compatibilityVersion = 1.7
sourceCompatibility = compatibilityVersion
targetCompatibility = compatibilityVersion

buildscript {
	gitHub = {
		def resolver = new URLResolver()
		resolver.with {
			name = "GitHub"
			addArtifactPattern "http://cloud.github.com/downloads/[organisation]/[module]/[module]-[revision].[ext]"
		}
		resolver
	}	
}

repositories {
	mavenCentral()
	add gitHub()
}

dependencies {
	def gaeVersion = "1.6.0"
	groovy "org.codehaus.groovy:groovy:1.8.4"
	compile "org.spockframework:spock-core:0.5-groovy-1.8", {
		exclude group: "org.codehaus.groovy", name: "groovy-all"
	}
	compile "glaforge:gaelyk:1.1"
	compile "com.google.appengine:appengine-api-1.0-sdk:${gaeVersion}"
	compile "com.google.appengine:appengine-api-labs:${gaeVersion}"
	compile "com.google.appengine:appengine-api-stubs:${gaeVersion}"
	compile "com.google.appengine:appengine-testing:${gaeVersion}"
	compile "javax.servlet:servlet-api:2.5"
	compile "cglib:cglib-nodep:2.2"
	compile "org.objenesis:objenesis:1.2"
}

idea {
	project {
		jdkName = compatibilityVersion

		ipr.withXml { provider ->
			def node = provider.asNode()

			// Set Gradle home
			def gradleSettings = node.appendNode('component', [name: 'GradleSettings'])
			gradleSettings.appendNode('option', [name: 'SDK_HOME', value: gradle.gradleHomeDir])
		}
	}
}
